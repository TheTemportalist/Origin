
// Using old gradle because ForgeGradle needs maven for snapshots,
// and because ForgeGradle doesn't like sourceSets outside of api, main, and test

buildscript {
	repositories {
		jcenter()
		maven {
			name "forge"
			url "http://files.minecraftforge.net/maven"
		}
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath "gradle.plugin.temportalist:AutomataGradle:0.0.1.31"
	}
}

// ~~~ Start: Gradle plugin application
plugins {
	id 'java'
	id 'scala'
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.32"
}
// ~~~~~ End: Gradle plugin application

// ~~~ Start: Java Compatibility
sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7
// ~~~~~ End: Java Compatibility

// ~~~ Start: SourceSets
sourceSets {
	foundation;
}

apply plugin: 'temportalist.automata' // Apply Automata

sourceSets {
	foundation.compileClasspath += api.compileClasspath
	foundation.compileClasspath += api.output;
	main.compileClasspath += foundation.compileClasspath;
	main.compileClasspath += foundation.output;
}

jar {
	from sourceSets.api.output;
	from sourceSets.foundation.output;
	from sourceSets.main.output;
}

task sourcesJar(type: Jar) {
	from sourceSets.api.scala;
	from sourceSets.foundation.scala;
	from sourceSets.main.scala;
	classifier = 'sources'
}

tasks.build.dependsOn sourcesJar

// ~~~~~ End: SourceSets

// ~~~ Start: Automata

ext {
	gitName = "Origin"
	gitBase = "https://github.com/TheTemportalist/" + project.gitName
	gitUrl = project.gitBase
	curseID = "72503"
}

def getBranch() {
	if (project.hasProperty("GIT_BRANCH"))
		return "${GIT_BRANCH}"
	else {
		def proc = "git rev-parse --abbrev-ref HEAD".execute()
		proc.waitFor()
		return proc.text.trim()
	}
}

def getDate() {
	def date = new Date()
	def formattedDate = date.format('MM_dd_yyyy_HH_mm_ss')
	return formattedDate
}

def getBuildNumber() {
	if (System.getenv("BUILD_NUMBER") != null) return "${System.getenv("BUILD_NUMBER")}"
	else if (project.hasProperty("bambooBuildNumber")) return project.bambooBuildNumber
	else return getDate()
}

automata {

	// ~~~ Start: Project ID
	organization = "temportalist"
	groupName = "origin"
	// ~~~~~ End: Project ID

	// ~~~ Start: Versioning
	versionMajor = 9
	versionMinor = 1
	versionPatch = 8

	if (project.hasProperty("MANUAL"))
		manualBuildNumber = MANUAL

	def branch = getBranch()
	if (project.hasProperty("build_type")) {
		if (build_type == "dev")
			autoBuildNumber = getBuildNumber()
	} else if (!branch.startsWith("master"))
		autoBuildNumber = getBuildNumber()
	// ~~~~~ End: Versioning

	// ~~~ Start: Forge Data
	versionMinecraft = "1.9.4"
	versionForge = "12.17.0.1963"
	versionMappings = "snapshot_20160518"
	// ~~~~~ End: Forge Data

	// ~~~ Start: Replacements
	replace { // MUST be defined after "Versioning" and "Forge Data"
		version = "@MOD_VERSION@"
		forge = 'required-after:Forge'
		replaceMap = [:]
	}
	// ~~~~~ End: Replacements

	// ~~~ Start: Resources
	resources = true
	// ~~~~~ End: Resources

	// ~~~ Start: CurseForge
	if (project.hasProperty("tempCurseAPI")) curseKey = tempCurseAPI
	curseProject {
		id = curseID
		releaseType = "release"
		changelog = file('changelog.html')
		changelogType = 'html'
		addGameVersion getVersionMinecraft() // Automata function
		addArtifact sourcesJar
	}
	// ~~~~~ End: CurseForge

	// ~~~ Start: Archives
	if (project.hasProperty("dddUser") && project.hasProperty("dddPass")) {
		archives {
			name = "Origin"
			description = project.gitName
			repoUrl = "http://doubledoordev.net:8082/artifactory/mods"
			repoUsername = dddUser
			repoPassword = dddPass
			sourceUrl = project.gitUrl
			scmUrl = "scm:git:git://github.com/TheTemportalist/" + project.gitName + ".git"
			issueTrackerType = "github"
			issueTrackerUrl = project.gitBase + "issues"
			licenseType = "Apache License Version 2.0"
			licenseUrl = project.gitUrl + "LICENSE"
			developers = [
					[
							"id": "TheTemportalist",
							"name": "The Temportalist",
							"role": "developer"
					]
			]
		}
	}
	// ~~~~~ End: Archives

}

// ~~~~~ End: Automata

// ~~~ Start: Changelogs
task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
	filePath = "changelog.html"
	untaggedName = "${project.version}"
	fromCommit = "dcb26de2989a86e8ccc6f00f6b213302c28cfe35"
	toRef = "HEAD"
	templateContent = file('changelog.min.html').getText("UTF-8")
}

afterEvaluate {
	tasks["curseforge${curseID}"].dependsOn.add(makeChangelog)
}

task release { // Done locally to execute a release on CI to curseforge
	doLast {

		def branch = getBranch()
		def version = automata.getVersionStr()

		/** Tag with version */

		("git tag " + version).execute().waitFor()

		/** Push to BRANCH */

		("git push -u origin " + branch + " --tags").execute().waitFor()

		/** Merge current branch with master */

		"git checkout master".execute().waitFor()
		("git merge " + branch).execute().waitFor()

		/** Push with content AND tags */

		"git push -u origin master --tags".execute().waitFor()

		/** Checkout BRANCH */

		("git checkout " + branch).execute().waitFor()

	}
}
// ~~~~~ End: Changelogs
